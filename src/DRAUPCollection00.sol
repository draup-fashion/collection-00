// SPDX-License-Identifier: MIT
//
// This software is developed by DRAUP, Inc. "DRAUP" is a trademark of DRAUP, Inc.
// in the United States and other countries. Nothing herein grants you any rights in
// or to DRAUP, Inc.'s trade names or trademarks, all of which are expressly
// reserved to DRAUP, Inc.
//
//
//
//                            DRAUP COLLECTION 00
//
//                          DRAUP X NICOLAS SASSOON
//
//                              SEEN ON SCREEN
//
//
//                                                           .,,
//                                                           ;░░▒▒
//                                                           ;░φφ░╚╠
//                                                       .░φ░░░░Γ╙╙
//                                                       '░░░░
//                                                           ░░░░
//                                                           ¡░░░░
//                                                           ¡░░░░
//                                                           ¡░░░¡░
//                                                           :░░▒░!-
//                                                           ¡░¡░▒░│-
//                                                       .│''░▒░┐'░
//                                                       ;''¡░╠▒░.'░
//                                                   .,,;'''¡░╠╠░░. ┌
//                                               .░░░░░░┐..¡¡░╠╠▒░┐ '.
//                                               .░│¡░░░░░░░¡░░░▒╠▒░░. '~
//                                               ;│.¡░░░░░░░░░░░░▒╠╠░░' .
//                                           ;░┌¡░░░░░░░░░░░░▒╠╠╠░░'.░⌐
//                                             ;░.¡░░φ▒▒▒▒▒░░░░▒▒╠╬▒░░\░░
//                                           ;░┐¡░φ╠╠╬╬╠╠▒▒▒▒▒╠╠╬╠▒░░░░
//                                       .░░.░φ▒╠╬╬╬╣╣╣╬╬╠╠╠╠╬╬▒░░░░
//                                       .░░│░░▒╠╠╠╠╬╬╬▓██▓▓╬╬╬╬▒░░░░
//                                       ░░░░φ▒╩╚╩╚╚Γ└''└│╙╙╚╩╩╩╚╙Γ"
//                                   .\¡░φ▒░░░░░░░       ''''''
//                                   ¡¡░▒░░░│││¡└
//                                   .¡░░░░░│'''¡'
//                               .░░░░░░░░┐''│¡~
//                       .,░φ░░░░░░░░░░░░░│..'!░
//                   .░░░░░░░░░░░░░░░░░░░░░░┐. !-
//                   ;░░φ▒▒▒▒░░░░░░░░░░░░░░░░░░░┐.'-
//               .░░φ▒▒▒▒░░░░░░▒▒▒▒╠╠╠▒▒▒▒▒▒▒φ░░░¡░.
//               .░░▒╠╠▒░░░░░░░░▒╠╠╩╩╩╩╩╩╬╬╣╣╣▓╣╬▒▒φ░░;
//               .¡░▒╠╠▒░░░░░░░░░░Γ░└'''''''""╙╙╩╬╬╬╬╣╣╬▒φ░»,
//               :░φ╠╬▒▒░░░░░░░░░░             '''!╙╚╩╠╠╠╠╠╠╠▒φφφ░,
//               :░▒╬╬▒▒░░░░░░░░'                   '!░░░╠╠╠╠╠╠╠▒▒▒░░░.
//               :░╠╬╬▒▒░░░░░░░'                     .░░░▒▒╠╠╠╠▒▒▒▒▒▒░░░.
//               '░╚╬╬▒▒░░░░░░                     .≥░░░▒▒▒▒▒▒▒▒▒▒▒▒▒░░░┐-
//               !░╠╬▒▒░░░░░                  ,;φ░░░░▒▒▒▒▒▒▒▒▒▒▒╠╠▒▒▒░░░┌.
//               ^░╠╬╠▒▒░░░               ,φ░░░░░░▒▒▒▒▒▒▒▒▒▒▒▒▒╠╠╠▒▒▒░░░│¡
//               !╚╬╬▒▒░▒░            »░░░░░░▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒╠╠╠╠╠▒▒▒░░░░░
//               '░╠╬▒▒▒▒░         ,φ░░░░▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒╠╠╠╠╠╠╠╠╠▒▒░░░░░
//                   '╚╠▒▒▒▒░       ;░░░▒▒▒╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠▒▒░░░░░
//                   ░╠▒▒▒▒▒░  .,φφ▒▒▒╠╠╠╬╬╬╬╬╬╬╬╬╬╬╬╠╠╠╬╬╬╬╬╬╬╬╬╬╠╠▒▒░░░░
//                   ]╠▒▒▒▒▒▒▒╠╠╠╠╠╠╠╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╠▒▒░░░'
//                   ]╠▒▒▒▒▒▒▒╠╠╠╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╠▒▒▒░'
//                   !╠╠▒▒▒▒▒▒╠╠╠╠╬╬╬╬╬╬╬╬╬╬╬╬╬╬╣▓▓▓▓▓▓▓▓▓▓▓▓╬╬╬╬╬╬╠╠╩░'
//                   '╙╬╬▒▒▒▒▒╠╠╠╠╠╠╬╬╬╬╬╬╬╬╬╬╬╬╣▓▓▓▓▓▓▓▓▓▓▓▓▓▓╬╬╬╬╩Γ"
//                   '!╚╣╬╬╠╠╠╠╠╠╠╬╬╬╬╬╬╬╬╬╬╬╬╬╬╣▓▓▓▓▓▓▓▓▓▓▓▓╬╬╩╙░'
//                       '░╚╣▓▓╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╣▓▓▓▓▀╩╩╙╙░└''
//                       '!╙╩╣▓▓╣╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╬╩╙░"''
//                           '!╙╚╬▓▓▓▓▓╬╬╬╬╬╬╬╬╬╬╩╙Γ"'
//
//
//
pragma solidity ~0.8.18;
import {Ownable} from "openzeppelin-contracts/contracts/access/Ownable.sol";
import {ERC721A} from "erc721a/contracts/ERC721A.sol";
import {DefaultOperatorFilterer} from "operator-filter-registry/src/DefaultOperatorFilterer.sol";
import {BitSplit} from "./BitSplit.sol";

// define constants for the item types
uint256 constant COAT_ITEM_TYPE = 0;
uint256 constant DRESS_ITEM_TYPE = 1;
uint256 constant PANTS_ITEM_TYPE = 2;
uint256 constant TOP_ITEM_TYPE = 3;
uint256 constant HAT_ITEM_TYPE = 4;

contract DRAUPCollection00 is ERC721A, Ownable, DefaultOperatorFilterer {
    using BitSplit for uint256;
    uint256[5] private maxSupplies;
    uint256[5] private mintPrices;
    string public baseTokenURI;

    mapping(uint256 => uint256) private _tokenItemTypes;

    mapping(uint256 => bytes32) private _tokenSeeds;

    constructor(uint256[5] memory setSupply, uint256[5] memory setMintPrices, string memory initialBaseURI) ERC721A("DRAUP COLLECTION 00", "DRAUP:00") {
        maxSupplies = setSupply;
        mintPrices = setMintPrices;
        baseTokenURI = initialBaseURI;
    }

    error InsufficientFunds(uint256 totalCost);
    error InsufficientItemSupply(uint256 itemType);
    error CannotMintItemType();
    error TooManyItems();

    function getMaxSupply() public view returns (uint256) {
        return maxSupplies[0] + maxSupplies[1] + maxSupplies[2] + maxSupplies[3] + maxSupplies[4];
    }

    function getItemMaxSupply(uint256 itemType) public view returns (uint256) {
        return maxSupplies[itemType];
    }

    function getItemSupply(uint256 itemType) public view returns (uint256) {
        return maxSupplies[itemType];
    }

    function tokenInfo(uint256 tokenId) public view returns (uint256 itemType, bytes32 seed) {
        itemType = _tokenItemTypes[tokenId];
        seed = _tokenSeeds[tokenId];
    }

    function tokenInfos(uint256 start, uint256 end) public view returns (uint256[] memory itemTypes, bytes32[] memory seeds) {
        if (end == 0) {
            end = _nextTokenId();
        }
        require(start < end, "start must be less than end");
        itemTypes = new uint256[](end-start);
        seeds = new bytes32[](end-start);
        for (uint256 i = start; i < end; i++) {
            itemTypes[i-start] = _tokenItemTypes[i];
            seeds[i-start] = _tokenSeeds[i];
        }
    }

    // hero pieces minted by DRAUP using short form generative techniques
    function mintCoats(address to, bytes32[] calldata seeds) public onlyOwner {
        uint256 startTokenId = _nextTokenId();
        uint256 itemType = COAT_ITEM_TYPE;
        if (seeds.length > maxSupplies[itemType]) {
            revert InsufficientItemSupply(itemType);
        }
        maxSupplies[itemType] -= seeds.length;
        for (uint i=0; i<seeds.length; i++) {
            _mint(to, 1);
            // item type is 0
            _tokenSeeds[startTokenId+i] = seeds[i];
        }
    }

    function mintCostForItems(uint256[] calldata items) public view returns (uint256 totalCost) {
        for (uint i=0; i<items.length; i++) {
            totalCost += mintPrices[items[i]];
        }
    }

    // main collection pieces minted by public using long form generative techniques
    function mintItems(address to, uint256[] calldata items) public payable {
        if (items.length > 4) {
            revert TooManyItems();
        }
        uint totalCost = mintCostForItems(items);
        if (msg.value < totalCost) {
            revert InsufficientFunds(totalCost);
        }
        uint batchStartTokenId = _nextTokenId();
        uint rand = block.prevrandao;
        uint itemType;
        for (uint i=0; i<items.length; i++) {
            itemType = items[i];
            if (itemType == 0 || itemType > 4) {
                revert CannotMintItemType();
            }
            if (maxSupplies[itemType] == 0) {
                revert InsufficientItemSupply(itemType);
            }
            maxSupplies[itemType] -= 1;
            if (items.length > 1) {
                uint[4] memory rquarters = rand.splitQuarter();
                if (items.length == 2) {
                    if (i == 0) {
                        rand = rquarters[0] | rquarters[1];
                    } else {
                        rand = rquarters[2] | rquarters[3];
                    }
                } else {
                    // NB: when batch size is 3 we drop the randomness contribution from the last quarter of randao
                    rand = rquarters[i];
                }
            }
            bytes32 seed = keccak256(abi.encodePacked(batchStartTokenId+i, rand, blockhash(block.number - 1), msg.sender));
            _tokenItemTypes[batchStartTokenId+i] = itemType;
            _tokenSeeds[batchStartTokenId+i] = seed;
            _mint(to, 1);
        }
    }

    // on-chain royalty enforcement integration
    function setApprovalForAll(address operator, bool approved) public override onlyAllowedOperatorApproval(operator) {
        super.setApprovalForAll(operator, approved);
    }

    function approve(address operator, uint256 tokenId) public override payable onlyAllowedOperatorApproval(operator) {
        super.approve(operator, tokenId);
    }

    function transferFrom(address from, address to, uint256 tokenId) public override payable onlyAllowedOperator(from) {
        super.transferFrom(from, to, tokenId);
    }

    function safeTransferFrom(address from, address to, uint256 tokenId) public override payable onlyAllowedOperator(from) {
        super.safeTransferFrom(from, to, tokenId);
    }

    function safeTransferFrom(address from, address to, uint256 tokenId, bytes memory data)
        public
        override
        payable
        onlyAllowedOperator(from)
    {
        super.safeTransferFrom(from, to, tokenId, data);
    }

}